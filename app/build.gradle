apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.wly.beansprout"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 130
        versionName "1.3.0"
    }

    //编译可选项参数
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    //签名配置
    signingConfigs {
        //正式
        release {
            //指定签名文件的别名
            keyAlias 'BeanSproutAssistant'

            //指定签名文件的别名配对密码
            keyPassword '123456789'

            //签名文件路径
            storeFile file('keystore/release.keystore')

            //签名文件的密码
            storePassword '123456789'
        }
        //测试
        debug {
            //签名文件路径
            storeFile file('keystore/debug.keystore')
        }
    }

    //构建类型(一般是release和debug，还可以自定义)
    buildTypes {
        //测试版
        debug {
            //默认false,是否开启断点调试
            debuggable true

            //默认false,是否开启jni的断点调试
            jniDebuggable true

            //版本名的后缀
            versionNameSuffix '.测试版'

            //applicationId的后缀,相当于更改了applicationId,可以认为是一个新的应用
            applicationIdSuffix '.test'

            //配置签名方式，这里配置会覆盖defaultConfig中配置的签名方式
            signingConfig signingConfigs.debug

            //SP文件名称
            buildConfigField "String", "SHARED_NAME_INVEST", "\"sharedAssistantTest\""

            //AndroidManifest中用到的配置
            manifestPlaceholders = [APP_NAME             : "@string/app_name_debug"]
        }

        //正式发布版
        release {
            //默认false,是否开启断点调试
            debuggable false

            //默认false,是否混淆的开关
            minifyEnabled true

            //Zipalign优化
            zipAlignEnabled true

            //默认false,是否支持本地化整理
            pseudoLocalesEnabled true

            //加载默认混淆配置文件proguard-rules.pro
            proguardFile 'proguard-rules.pro'

            //配置签名方式，这里配置会覆盖defaultConfig中配置的签名方式
            signingConfig signingConfigs.release

            //SP文件名称
            buildConfigField "String", "SHARED_NAME_INVEST", "\"sharedAssistantTest\""

            //AndroidManifest中用到的配置
            manifestPlaceholders = [APP_NAME             : "@string/app_name"]
        }
    }

    //打包时生成指定的APK命名格式
    android.applicationVariants.all { variant ->
        variant.outputs.all {
            // 项目名-编译类型-版本号.apk
            def appName = "豆芽助手";
            def filesuffix = variant.buildType.name;
            def fileName = "${appName}-${filesuffix}-${versionName}.apk"

            println "自定义输出apk的名字:" + fileName;
            outputFileName = fileName;

        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.18'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.7.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.7.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.2'
    implementation 'net.grandcentrix.tray:tray:0.12.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.jakewharton.rxbinding3:rxbinding:3.0.0'

    // 检查更新
    implementation project(path: ':checkupdatelib')
    // 对话框(https://github.com/afollestad/material-dialogs)
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    // 状态栏
    implementation 'com.github.niorgai:StatusBarCompat:2.3.3'
}
